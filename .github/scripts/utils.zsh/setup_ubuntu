autoload -Uz log_error log_status log_info mkcd

if (( ! ${+project_root} )) {
  log_error "'project_root' not set. Please set before running ${0}."
  return 2
}

if (( ! ${+target} )) {
  log_error "'target' not set. Please set before running ${0}."
  return 2
}

pushd ${project_root}

typeset -g QT_VERSION

local -a apt_args=(
  ${CI:+-y}
  --no-install-recommends
)
local -a apt_cache_args=()
if (( _loglevel == 0 )) apt_args+=(--quiet)

if [[ -n "${APT_ARCHIVE_DIR:-}" ]] {
  mkdir -p "${APT_ARCHIVE_DIR}/partial"
  apt_cache_args=(-o "Dir::Cache::archives=${APT_ARCHIVE_DIR}")
}

if (( ! (${skips[(Ie)all]} + ${skips[(Ie)deps]}) )) {
  log_group 'Installing obs-studio build dependencies...'

  local suffix
  if [[ ${CPUTYPE} != "${target##*-}" ]] {
    local -A arch_mappings=(
      aarch64 arm64
      x86_64 amd64
    )

    suffix=":${arch_mappings[${target##*-}]}"

    sudo apt-get ${apt_cache_args} ${apt_args} install gcc-${${target##*-}//_/-}-linux-gnu g++-${${target##*-}//_/-}-linux-gnu
  }

  sudo add-apt-repository --yes ppa:obsproject/obs-studio
  sudo apt update

  local -a _qt_packages=()

  if (( QT_VERSION == 5 )) {
    _qt_packages+=(
      qtbase5-dev${suffix}
    )
  } else {
    _qt_packages+=(
      qt6-base-dev${suffix}
    )
  }

  sudo apt-get ${apt_cache_args} ${apt_args} \
    build-essential \
    libgles2-mesa-dev \
    libsimde-dev \
    obs-studio \
    ${_qt_packages}
  log_group
}
